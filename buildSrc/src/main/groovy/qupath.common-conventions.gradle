plugins {
    id 'qupath.java-conventions'
    id 'jacoco'
    id 'io.github.qupath.platform'
    id 'org.bytedeco.gradle-javacpp-platform'
    id 'checkstyle'
}

repositories {
    mavenCentral()

    // May be required for snapshot ImageJ2 jars
    maven {
        name 'ImageJ'
        url 'https://maven.imagej.net/content/groups/public'
    }

    // Required for Bio-Formats
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        name 'Open Microscopy'
        url 'https://artifacts.openmicroscopy.org/artifactory/maven/'
    }

    // May be required for snapshot JavaCPP jars
    maven {
        name 'Sonatype snapshots'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    // Currently required for OpenSlide
    maven { url "../maven/repo" }

}


/*
 * Some metadata for the manifest
 */
project.version = gradle.ext.qupathVersion

/*
 * Optionally use OpenCV with CUDA.
 * See https://github.com/bytedeco/javacpp-presets/tree/master/cuda for info (and licenses).
 */
def useCudaRedist = project.hasProperty('cuda-redist')
def useCuda = useCudaRedist || project.hasProperty('cuda')

/*
 * Handle OS-specific decisions
 */
String platform = properties['platform.shortName']
if (properties['platform.name'] == null)
    logger.warn('Unknown operating system!')
if ("32".equals(System.getProperty("sun.arch.data.model"))) {
    logger.warn("You appear to be using a 32-bit JDK - it is very possible some things won't work!")
    logger.warn("You may at least need to replace the OpenSlide dlls with 32-bit versions from https://openslide.org/download/")
}

// Check if we are running using Apple Silicon
//String osName = System.properties['os.name']?.toLowerCase()
String osArch = System.properties['os.arch']
def isAppleSilicon = platform == 'mac' && osArch == 'aarch64'
if (isAppleSilicon) {
    project.logger.warn("""
Apple Silicon support is experimental and incomplete!
I will try to build anyway, but note that:
- I may need to use a SNAPSHOT version of OpenCV (subject to change on each build)
- Parts of the software relying on native libraries are unlikely to work (including OpenSlide)
If you want to avoid these messages, please use a JDK for x86_64.
	""".trim())
}


/*
 * Preserve the version number
 */
project.file('src/main/resources/VERSION').setText(gradle.ext.qupathVersion, 'UTF-8')


configurations {
    opencv
    guava
}

dependencies {

    if (useCudaRedist) {
        opencv libs.bundles.opencv.cuda
    } else if (useCuda) {
        opencv libs.bundles.opencv.gpu
    } else
        opencv libs.bundles.opencv


    guava libs.guava, {
        exclude group: 'com.google.code.findbugs'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.checkerframework', module: 'checker-qual'
    }

}


dependencies {
    testImplementation libs.junit
    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine

    implementation libs.bundles.logging
}

tasks.named('test') {
    useJUnitPlatform()
}