plugins {
  id 'qupath.extension-conventions'
  id 'qupath.publishing-conventions'
  id 'java-library'

  alias(libs.plugins.javafx)
}

ext.moduleName = 'qupath.extension.bioformats'
base {
  archivesName = 'qupath-extension-bioformats'
  description = 'QuPath extension to support image reading and writing using Bio-Formats.'
}

def bioformatsVersion = libs.versions.bioformats.get()
def versionOverride = project.properties.getOrDefault('bioformats-version', null)
if (versionOverride) {
	println "Using specified Bio-Formats version ${versionOverride}"
	bioformatsVersion = versionOverride
}

String nativesClassifier = properties['platform.classifier']
if (nativesClassifier == 'natives-darwin-aarch64') {
	println "WARNING! Bio-Formats does not fully support Apple Silicon (many .czi and some .ndpi images are known to fail)"
}


dependencies {
	// This can be used to include bioformats_package.jar - however it causes warnings with SLF4J
//  implementation("ome:bioformats_package:${bioformatsVersion}") {
//  	transitive = false
//  }

  implementation libs.qupath.fxtras
  implementation libs.controlsfx
  implementation libs.picocli
  implementation libs.jna           // needed for OMEZarrReader (see https://github.com/bcdev/jzarr/issues/31)

  implementation "ome:formats-gpl:${bioformatsVersion}", {
    exclude group: 'xalan', module: 'serializer'
    exclude group: 'xalan', module: 'xalan'
    exclude group: 'io.minio', module: 'minio'
    exclude group: 'commons-codec', module: 'commons-codec'
    exclude group: 'commons-logging', module: 'commons-logging'
//        exclude group: 'edu.ucar', module: 'cdm'
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'com.google.code.findbugs', module: 'annotations'
  }
  implementation group: 'ome', name: 'OMEZarrReader', version: libs.versions.omeZarrReader.get()
  implementation "io.github.qupath:blosc:${libs.versions.blosc.get()}:${nativesClassifier.replace("natives-", "")}"

//  testImplementation("ome:bioformats_package:${bioformatsVersion}")
  testImplementation "ome:bio-formats_plugins:${bioformatsVersion}"
  
  testImplementation libs.imagej
  
}
